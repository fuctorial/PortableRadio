buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "ForgeGradlePatch"
            url  = "https://github.com/juanmuscaria/maven/raw/master"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:[1.2-1.4.6-SNAPSHOT,)"
    }
}

repositories {
    flatDir { dirs 'deps' }
    mavenCentral()
    maven { name = "MinecraftForge"; url = "https://maven.minecraftforge.net/" }
    maven { name = "Minecraft";      url = "https://libraries.minecraft.net/" }
    maven { name = "Sonatype";       url = "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { name = "CurseForge";     url = "https://www.cursemaven.com" }
    jcenter()
}

apply plugin: 'forge'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"
version = "1.0.0"
group   = "ru.fuctorial.portableradio"
archivesBaseName = "PortableRadio"

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "minecraft-debug"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

afterEvaluate {
    downloadMcpTools.enabled = false
    task copyMcpFile(type: Copy) {
        from "${projectDir}/mcp/mcp_stable-12-1.7.10.zip"
        into "${gradle.gradleUserHomeDir}/caches/minecraft/de/oceanlabs/mcp/mcp_stable/12-1.7.10/"
        rename { "mcp_stable-12-1.7.10.zip" }
    }
    tasks.matching { it.name == 'extractMcpData' }.all {
        dependsOn copyMcpFile
        enabled = false
    }
}

configurations {
    embed
    compile.extendsFrom embed
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile fileTree(dir: 'deps', include: '*.jar')
}

jar {
    from {
        configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

build.dependsOn jar

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}